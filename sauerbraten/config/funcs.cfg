createine = [
    if (strcmp "" (getalias $arg1)) [
        alias $arg1 $arg2;
    ]
]

isinlist = [
    alias retval 0
    alias varsel 2
    while [= 0 (strcmp (getalias (concatword arg $varsel)) "")] [
        list = (getalias (concatword arg $varsel))
        loop i (listlen $list) [
            if (= 1 (strcmp $arg1 (at $list $i ))) [
                retval = 1
            ]
        ]
        varsel = (+ $varsel 1)
    ]
    result $retval
]

checkwcng = [
    if (= $numargs 0) [
        if (= (getvarmin wcversion) 1200) [
            return_val = (wcver ">=" "0.7.3")
        ] [
            return_val = 0
        ]
    ] [
        if (= (getvarmin wcversion) 1200) [
          return_val = (wcver $arg1 $arg2)
        ] [
          return_val = 0
        ]
    ]

    result $return_val
]

if (= (checkwcng) 1) [
    iswindows = (> (strstr (getosstring) "win") -1)
    islinux = (> (strstr (getosstring) "linux") -1)
    isosx = (> (strstr (getosstring) "osx") -1)
]

//color escaping
f_escape_color = [
    result (strreplace $arg1 "^f" "^^f")
]

f_unescape_color = [
    result (strreplace $arg1 "^^f" "^f")
]

storedisplayvars = [
    cspdisplay = $showplayerdisplay
    cssdisplay = $showstatsdisplay
    csstldisplay = $showtimeleft
    csndisplay = $shownetworkdisplay

    showplayerdisplay 0
    showstatsdisplay 0
    showtimeleft 0
    shownetworkdisplay 0
]

restoredisplayvars = [
    showplayerdisplay $cspdisplay
    showstatsdisplay $cssdisplay
    showtimeleft $csstldisplay
    shownetworkdisplay $csndisplay
]

// list splitter
//   arg1: output of name
//   arg2: output of value
//   arg3: max columns
//   arg4: format list in format <name1> <value1> ... <namen> <valuen>
//   arg5: code to execute / callback
f_gui_list_split2 = [
    guilist [
        i = 0
        l = (listlen $arg4)
        c = (min (div $l 2) $arg3)
        r = (div (+ $l (- $c 1)) $c)
        guispring
        loop a $c [
            guilist [
                t = (min (+ $i $r) $l)
                while [< $i $t] [
                    $arg1 = (at $arg4 $i)
                    $arg2 = (at $arg4 (+ $i 1))
                    arg5
                    i = (+ $i  2)
                ]
            ]
        ]
        guispring
    ]
]

// color and format choice dialog
//   arg1: input
//   arg2: max columns
//   arg3: format list in format <name1> <value1> <name2> <value2> ...
//   arg4: code to execute / callback with new value as arg1 - arg1 isn't set on Cancel
f_color_format_choice = [
    wcok = (checkwcng)
    code = (f_escape_color $arg1)
    orig = $code
    cols = $arg2
    fmts = $arg3
    func = $arg4

    fmtcolors = [
        "^f0Green"       "^f0" "^f6Orange"  "^f6"
        "^f1Blue"        "^f1" "^f7White"   "^f7"
        "^f2Yellow"      "^f2" "^f8Black"   "^f8"
        "^f3Red"         "^f3" "^f5Purple"  "^f5"
        "^f4Gray"        "^f4" "^f9Silver"  "^f9"
        "^f7Store"       "^fs" "^f7Restore" "^fr"
    ]

    f_color_format_choice_update = [
        if $wcok [
            textfocus "#pastebuffer"; textset $arg1
            textfocus "#fmt"; textpaste
        ] [
            code = (concatword $code $arg1)
        ]
    ]

    newgui "Color / Format Choice" [
        guistayopen [
        guitext "Colors" 0
        f_gui_list_split2 n v 7 $fmtcolors [
            guibutton (concat (f_unescape_color $n) " ") [ f_color_format_choice_update @v ]
        ]
        if (listlen $fmts) [
            guibar
            guitext "Formats" 0

            f_gui_list_split2 n v $cols $fmts [
                guibutton (concat $n " ") [ f_color_format_choice_update @v ]
            ]
        ]
        guibar
        guitext "Code" 0
        if $wcok [
            guinoautotab [
                guieditor "#fmt" -70 2
            ]
            code = (textcurrentline)
        ] [
            guifield code 70
        ]
        guitext "Result" 0
        guitextbox (? (strcmp $code "") " " (f_unescape_color $code)) 70 1 0xFFFFFF
        guibar
        guialign 0 [
            if $wcok [
            guibutton "Copy " [ textfocus "#fmt"; textcopy ]
            guibutton "Paste" [ textfocus "#fmt"; textpaste ]
            guibar
            ]
            guibutton "Reset "  [ code = $orig; if $wcok [ textfocus "#fmt"; textclear; textset $orig ] ]
            guibutton "Clear "  [ code = "";    if $wcok [ textfocus "#fmt"; textclear ] ]
            guibar
            guibutton "Cancel " [ cleargui 1; func ]
            guibutton "Done"    [ cleargui 1; func (f_unescape_color $code) ]
        ]
    ]
    ]

    showgui "Color / Format Choice"

    if $wcok [
        textfocus "#fmt"
        textset $code
    ]
]

colorchooser = [
    cr = (? (> $numargs 0) $arg1 tmp)
    f_color_format_choice $arg2 0 "" [ if (> $numargs 0) [ $cr = $Ð°rg1 ] ]
]
