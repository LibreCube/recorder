if ( = (checkwcng) 1) [

///////////////////////////////////////////////////////
// Common
///////////////////////////////////////////////////////

newgui "PluginError" [
    if (> $numargs 0) [ n = $arg1 ] [ n = "unknown" ]
    guitext "^f3ERROR:" 0
    guitext (format "Plugin %1 is not available." $n) 0
]

f_load_plugin = [
    if (> $numargs 0) [
        if (ispluginloaded $arg1) [ result 1 ] [ result (loadplugin $arg1) ]
    ] [
        result 0
    ]
]

f_complete_plugin = [
    if (> $numargs 1) [
        complete $arg1 plugins $arg2
    ]
]

f_show_plugin_gui = [
    if (&& (> $numargs 1) (f_load_plugin $arg1)) [
        showgui $arg2
    ] [
        showgui "PluginError"
    ]
]

f_get_plugin_name = [
    result (at (at $wc_plugins $arg1) 0)
]

f_get_plugin_desc = [
    result (at (at $wc_plugins $arg1) 1)
]

wc_plugins = ""

f_registerplugin = [
    wc_plugins = (format "%1 ^" %2 ^"" $wc_plugins $arg1)
]

f_update_plugins = [
    loop i (listlen $wc_plugins) [
        if (getalias (concatword "plugin_" (f_get_plugin_name $i) "_loaded")) [
            if (! (ispluginloaded (f_get_plugin_name $i))) [
                loadplugin (f_get_plugin_name $i)
            ]
        ] [
            if (ispluginloaded (f_get_plugin_name $i)) [
                unloadplugin (f_get_plugin_name $i)
            ]
        ]
    ]
]

f_update_plugin_status = [
    loop i (listlen $wc_plugins) [
        (concatword "plugin_" (f_get_plugin_name $i) "_loaded") = (ispluginloaded (f_get_plugin_name $i))
    ]
]

///////////////////////////////////////////////////////
// AutoLoad
///////////////////////////////////////////////////////

f_plugin_autoload = [
    loop i (listlen $wc_plugins) [
        if (getalias (concatword "plugin_" (f_get_plugin_name $i) "_autoload")) [
            f_load_plugin (f_get_plugin_name $i)
        ]
    ]
]

f_plugin_autoinit = [
    local pconf
    local plist
    local ppath
    local pext
    plist = ""
    ppath = (format "%1/plugins/" $config_dir)

    loopfiles f $ppath cfg [
        pconf = (concatword $f ".cfg")
        if (&& (! (isinlist $pconf $plist)) (! (strcmp $pconf "plugins.cfg"))) [
            plist = (concat $plist $pconf)
            exec (format "%1/%2" $ppath $pconf)
        ]
    ]

    if (iswindows) [ pext = "dll" ] [
    if (isosx)     [ pext = "dylib" ] [
                     pext = "so" ] ]

    f_complete_plugin "loadplugin" $pext
    f_complete_plugin "unloadplugin" $pext
    f_complete_plugin "getplugin" $pext
    f_complete_plugin "setplugin" $pext
    f_complete_plugin "requireplugin" $pext
]

f_plugin_autoinit
f_plugin_autoload

]
