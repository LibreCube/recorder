///////////////////////////////////////////////////////
// Extinfo Player Search Gui
///////////////////////////////////////////////////////

if ( = (checkwcng) 1) [

extinfo_player_name = ""
extinfo_player_country = ""
extinfo_frame_counter = 0
extinfo_result_count = 0
extinfo_total_playercount = 0
extinfo_player_search_show_percent = 1
extinfo_players_per_page = 18
extinfo_var_count = 0
extinfo_player_search_guiname = "player search"


extinfo_country_click = [
    // arg1 = country code
    extinfo_player_country = $arg1
    extinfo_frame_counter = 0
]


extinfo_renderplayer = [
    local x
    local end
    end = (+ $arg1 $arg2)

    guilist [
    guilist [
    guilist [

        gui_render_server "server name" $arg1 $end extinfo_server_names extinfo_server_addresses

        guilist [
            guititleleftalign "player"
            x = $arg1
            local player
            local player_escaped
            while [(< $x $end)] [
                guistayopen [
                    player = (scriptvector extinfo_player_names $x)
                    player_escaped = (escape $player)
                    guibutton $player [extinfo_player_name = @player_escaped; extinfo_frame_counter = 0] 0
                ]
                x = (+ $x 1)
            ]
            guistrut 14 1
        ]

        gui_render_country "country" $arg1 $end extinfo_country_codes extinfo_country_names extinfo_country_click

        guilist [
            guititleleftalign "ping"
            x = $arg1
            while [(< $x $end)] [
                guitext (scriptvector extinfo_player_pings $x) 0
                x = (+ $x 1)
            ]
            guistrut 5 1
        ]

        if ($showfrags) [
            guilist [
                guititleleftalign "frags"
                x = $arg1
                while [(< $x $end)] [
                    guitext (scriptvector extinfo_player_frags $x) 0
                    x = (+ $x 1)
                ]
                guistrut 5 1
            ]
        ]

        if ($showdeaths) [
            guilist [
                guititleleftalign "deaths"
                x = $arg1
                while [(< $x $end)] [
                    guitext (scriptvector extinfo_player_deaths $x) 0
                    x = (+ $x 1)
                ]
                guistrut 6 1
            ]
        ]

        if ($showacc) [
            guilist [
                guititleleftalign "acc"
                x = $arg1
                while [(< $x $end)] [
                    guitext (format "%1%%" (scriptvector extinfo_player_accs $x)) 0
                    x = (+ $x 1)
                ]
                guistrut 6 1
            ]
        ]

        if ($showkpd) [
            guilist [
                guititleleftalign "kpd"
                x = $arg1
                local deaths
                while [(< $x $end)] [
                    deaths = (scriptvector extinfo_player_deaths $x)
                    guitext (roundf (divf (scriptvector extinfo_player_frags $x) (? (!= $deaths 0) $deaths 1)) 2) 0
                    x = (+ $x 1)
                ]
                guistrut 6 1
            ]
        ]

        if ($showtks) [
            guilist [
                guititleleftalign "teamkills"
                x = $arg1
                while [(< $x $end)] [
                    guitext (scriptvector extinfo_player_tks $x) 0
                    x = (+ $x 1)
                ]
                guistrut 8 1
            ]
        ]

        guilist [
            guititleleftalign "cn"
            x = $arg1
            while [(< $x $end)] [
                guitext (scriptvector extinfo_player_cns $x) 0
                x = (+ $x 1)
            ]
        ]

    ]
    ]
    ]
]

newgui $extinfo_player_search_guiname [
    local varcount

    varcount = $showcountry
    varcount = (+ $varcount $showfrags)
    varcount = (+ $varcount $showdeaths)
    varcount = (+ $varcount $showacc)
    varcount = (+ $varcount $showkpd)
    varcount = (+ $varcount $showtks)

    if (!= $varcount $extinfo_var_count) [
        extinfo_var_count = $varcount
        extinfo_frame_counter = 0
    ]

    if (= (mod $extinfo_frame_counter 20) 0) [
        extinfo_total_playercount = (getextinfoplayercount)

        // create/clear vectors

        scriptvector extinfo_server_addresses
        scriptvector extinfo_player_names
        if ($showcountry) [
            scriptvector extinfo_country_codes
            scriptvector extinfo_country_names
        ]
        scriptvector extinfo_player_pings
        if (|| $showfrags $showkpd) [ scriptvector extinfo_player_frags ]
        if (|| $showdeaths $showkpd) [ scriptvector extinfo_player_deaths ]
        if ($showacc) [ scriptvector extinfo_player_accs ]
        if ($showtks) [ scriptvector extinfo_player_tks ]
        scriptvector extinfo_player_cns
        scriptvector extinfo_server_names

        extinfo_result_count = 0

        // callback args:
        // hostname hostint port serverping serverdesc secleft numplayers maxplayers mastermode gamemode mapname gamepaused gamespeed
        // cn ping playername playerteam frags flags deaths teamkills acc health armour gunselect priv state playercountrycode playercountry

        local callbackargs
        callbackargs = "server_ip ignore server_port ignore server_name ignore8 player_cn player_ping player_name ignore player_frags ignore player_deaths player_tks player_acc ignore5 country_code country_name"

        loopextinfoplayers $extinfo_player_name $extinfo_player_country $callbackargs [
            if (< $player_cn 128) [
                server_address = (format "%1 %2" $server_ip $server_port)
                scriptvector extinfo_server_addresses $extinfo_result_count $server_address
                scriptvector extinfo_player_names $extinfo_result_count (? (=s $player_name "") "-" $player_name)
                if ($showcountry) [
                    scriptvector extinfo_country_codes $extinfo_result_count (? (=s $country_code "") "??" $country_code)
                    scriptvector extinfo_country_names $extinfo_result_count (? (=s $country_name "") "??" $country_name)
                ]
                scriptvector extinfo_player_pings $extinfo_result_count $player_ping
                if (|| $showfrags $showkpd) [ scriptvector extinfo_player_frags $extinfo_result_count $player_frags ]
                if (|| $showdeaths $showkpd) [ scriptvector extinfo_player_deaths $extinfo_result_count $player_deaths ]
                if ($showacc) [ scriptvector extinfo_player_accs $extinfo_result_count $player_acc ]
                if ($showtks) [ scriptvector extinfo_player_tks $extinfo_result_count $player_tks ]
                scriptvector extinfo_player_cns $extinfo_result_count $player_cn
                scriptvector extinfo_server_names $extinfo_result_count (? (=s $server_name "") $server_address $server_name)

                extinfo_result_count = (+ $extinfo_result_count 1)
            ]
        ]
    ]

    extinfo_frame_counter = (+ $extinfo_frame_counter 1)

    local header
    local playersleft
    local c

    local percent
    if ($extinfo_player_search_show_percent) [
        local playercount
        playercount = $extinfo_total_playercount
        if (! $playercount) [ playercount = 1 ]
        percent = (format " (%1%%)" (roundf (*f (divf 100 $playercount) $extinfo_result_count) 2))
    ] [
        percent = ""
    ]

    header = [
        guialign 0 [
            guitext "Player " 0
            guifield extinfo_player_name 15 [extinfo_frame_counter = 0]
            guibar
            guitext "Country " 0
            guifield extinfo_player_country 2 [extinfo_frame_counter = 0]
            guibar
            guitext (format "%1 Player%2%3" $extinfo_result_count (? (!= $extinfo_result_count 1) "s" "") $percent) 0
        ]
        guibar
    ]

    playersleft = $extinfo_result_count

    header

    while [(> $playersleft 0)] [
        if (> $c 0) [
            guitab (+ $c 1)
            header
        ]
        extinfo_renderplayer (* $extinfo_players_per_page $c) (min $extinfo_players_per_page $playersleft)
        playersleft = (- $playersleft $extinfo_players_per_page)
        c = (+ $c 1)
    ]

    processservers
]

scriptevent "closegui" "guiname" [

    if (=s $guiname $extinfo_player_search_guiname) [

        delscriptvector extinfo_server_addresses
        delscriptvector extinfo_player_names
        delscriptvector extinfo_country_codes
        delscriptvector extinfo_country_names
        delscriptvector extinfo_player_pings
        delscriptvector extinfo_player_frags
        delscriptvector extinfo_player_deaths
        delscriptvector extinfo_player_accs
        delscriptvector extinfo_player_tks
        delscriptvector extinfo_player_cns
        delscriptvector extinfo_server_names

        extinfo_frame_counter = 0
    ]

]

searchplayer = [
    extinfo_player_name = $arg1
    extinfo_player_country = $arg2
    showgui $extinfo_player_search_guiname
]
findplayer = $searchplayer
searchcountry = [ searchplayer "" $arg1 ]
findcountry = $searchcountry

///////////////////////////////////////////////////////
// Extinfo Player Gui
///////////////////////////////////////////////////////

newgui "extinfo" [
    guistayopen [
        guinoautotab [
            //add additional stuff here
            guiextinfo //render the extinfo gui
        ]
    ]
]
] // end check wc