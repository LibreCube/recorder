// displays a warning whenever a reliable packet gets lost
// type /packetloss to display the total count of lost packets,
// since this script was added

if (> (getvarmin wcversion) 0) [

ifnotdeclared "packetlosscounter" [ packetlosscounter = 0 ]
scriptevent "disconnect" [ packetslostlast = 0 ]
packetslostlast = 0

scriptevent "bandwidthupdate" "ignore8 packetslost" [
    nowlost = (- $packetslost $packetslostlast)
    if (> $nowlost 0) [
        echo (concat "^f3WARNING:^f7 Lost" $nowlost "Packet(s)!")
        packetlosscounter = (+ $packetlosscounter $nowlost)
        packetslostlast = $packetslost
    ]
]

packetloss = [
    echo (concat "Lost" $packetlosscounter (concatword "Packet" (? (!= $packetlosscounter 1) "s" "")))
]

scriptevent "scriptinit" [ echo "^f6Packetloss Counter Extension loaded." ]

]