// adds flagrun time to the score message

if (> (getvarmin wcversion) 0) [

timetrialdecimals = 3

scriptevent "mapstart" "modename" [
    if (> (strstr $modename "ctf") -1) [
        looplist cn (listclients 1 1) [ scriptvector "timetrial" $cn -1 ]
    ]
]

scriptevent "connect" [ scriptvector "timetrial" ]
scriptevent "disconnect" [ delscriptvector "timetrial" ]
scriptevent "playerconnect" "player_cn" [ scriptvector "timetrial" $player_cn -1 ]
scriptevent "playerdisconnect" "player_cn" [ delscriptvectorindex "timetrial" $player_cn ]
scriptevent "flagtake" "player_cn" [ scriptvector "timetrial" $player_cn (getlastmillis) ]
scriptevent "flagdrop" "player_cn" [ scriptvector "timetrial" $player_cn -1 ]

scriptevent "flagscore" "player_cn ignore3 wasdropped" [
    if (! $wasdropped) [
        takemillis = (scriptvector "timetrial" $player_cn)
        
        if (>= $takemillis 1) [
            flagruntime = (roundf (divf (- (getlastmillis) $takemillis) 1000) $timetrialdecimals)
            result (concatword " (" $flagruntime " sec)")
        ]
    ]
]

scriptevent "scriptinit" [ echo "^f6CTF Timetrial Extension loaded." ]

]