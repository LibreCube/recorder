if ( = (checkwcng) 1) [

newgui "demo recorder" [
    guistayopen [
        guialign 0 [
            guilist [

                guicheckbox "record extinfo" clientdemorecordextinfo
                guicheckbox "skip server messages" clientdemoskipservmsg
                guicheckbox "skip command replies" clientdemoskipservcmds
                guicheckbox "record demos automatically" clientdemoautorecord
                
                guibar

                guitext "compression level"
                guislider clientdemocompresslevel

                if ((isconnected 0 0)) [
                    guibar
                    
                    if (! (clientdemorecording)) [
                        guibutton "record demo" [ recordclientdemo ]
                    ] [
                        guitext (format "raw size: %1 kb" (div (clientdemorawsize) 1024))
                        guibutton "stop demo recording" [ stopclientdemo ]
                    ]
                ]
            ]
        ]
    ]
]

demorecordergui = [ showgui "demo recorder" ]

search_demo_gamemode = ""
search_demo_servername = ""
search_demo_mapname = ""
search_demo_name = ""
search_demo_frags = -1
search_demo_deaths = -1
search_demo_acc = -1
search_demo_tks = -1
search_demo_flags = -1
search_demo_priv = -1

newgui "search demo" [
    guistayopen [
        guialign 0 [
            guilist [
                guitext "Player Name: "
                guifield search_demo_name 18 [search_demo_name = $search_demo_name]
                guibar
            ]
            guibar
            guilist [
                guitext "Frags >= "
                guifield search_demo_frags 3 [search_demo_frags = $search_demo_frags]
                guibar
                guitext "Flags >= "
                guifield search_demo_flags 3 [search_demo_flags = $search_demo_flags]
            ]
            guibar
            guilist [
                guitext "Deaths >= "
                guifield search_demo_deaths 3 [search_demo_deaths = $search_demo_deaths]
                guibar
                guitext "TKs >= "
                guifield search_demo_tks 3 [search_demo_tks = $search_demo_tks]
            ]
            guibar
            guilist [
                guitext "Acc >= "
                guifield search_demo_acc 3 [search_demo_acc = $search_demo_acc]
                guibar
                guitext "Priv >= "
                guifield search_demo_priv 3 [search_demo_priv = $search_demo_priv]
            ]
        ]
        
        guibar

        guialign 0 [
            guilist [
                guitext "Game Mode: "
                guifield search_demo_gamemode 15 [search_demo_gamemode = $search_demo_gamemode]
            ]
            guibar
            guilist [
                guitext "Server Name: "
                guifield search_demo_servername 15 [search_demo_servername = $search_demo_servername]
            ]
            guibar
            guilist [
                guitext "Map Name: "
                guifield search_demo_mapname 15 [search_demo_mapname = $search_demo_mapname]
            ]
        ]

        guibar

        guialign 0 [


            guitext "Min Demo Size (KB) "
            guifield searchdemomindemosize 4 []

            guibar

            guitext "Max Results "
            guifield searchdemomaxresults 3 []
            guibar

            guitext "Threads "
            guifield searchdemothreadlimit 2 []
        ]

        guibar

        guialign 0 [
            if (! (searchdemoactive)) [
                guibutton "Search Demos" [
                    searchdemo $search_demo_gamemode $search_demo_servername $search_demo_mapname $search_demo_name $search_demo_frags $search_demo_deaths $search_demo_acc $search_demo_tks $search_demo_flags $search_demo_priv
                ]
            ] [
                guibutton "Stop Searching" [
                    stopsearchdemo
                ]
            ]
        ]

    ]
]

searchdemogui = [ showgui "search demo" ]

update_demo_list = [
    demos = ""
    demodir = (clientdemogetdemodir)

    loopmodes curmode [
        dir = (concatword $demodir "/" (strreplace $curmode " " "_") "/")

        loopfiles curdemo $dir "dmo" [
            demos = (concatword $demos " " (concatword $dir $curdemo))
        ]
    ]

    loopfiles curdemo "." "dmo" [
        demos = (concatword $demos " " $curdemo)
    ]

    listcomplete "demo" $demos
    demos = ""
]

if (> (getvarmin wcversion) 0) [
    scriptevent "startup" [ sleep 1 [ update_demo_list ] ]
    scriptevent "clientdemoend" $update_demo_list
]
] // end check wc