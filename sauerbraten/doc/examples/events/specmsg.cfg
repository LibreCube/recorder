// displays a message whenever a player joins or leaves spectator mode

if (> (getvarmin wcversion) 0) [

specjoinmsg  = "^f0%1 (%2) ^f7joined spectators"
specleavemsg = "^f0%1 (%2) ^f7left spectators"


specmsg = [
    if (=s $arg3 "") [
        local fmt
        fmt = (escape $arg1)
        scriptevent "playerconnect" "player_cn player_name" [
            if (= $player_cn @arg2) [
                local player_escaped
                local eventid
                player_escaped = (escape $player_name)
                eventid = (getthiseventid)
                sleep 0 [
                    echo (format @@@fmt @player_escaped @player_cn)
                    delscriptevent @eventid
                ]
            ]
        ]
    ] [
        echo (format $arg1 $arg3 $arg2)
    ]
]

scriptevent "playerjoinspec" "player_cn player_name" [
    specmsg $specjoinmsg $player_cn $player_name
]

scriptevent "playerleavespec" "player_cn player_name" [ 
    specmsg $specleavemsg $player_cn $player_name
]

scriptevent "scriptinit" [ echo "^f6Spectator Messages Extension loaded." ]

]